10 CLS
20 PRINT "THIS PROGRAM ALLOWS YOU TO ADD, EDIT, DELETE, OR PRINT DATA"
30 PRINT "IN STATION FILE (SD.DAT)"
40 DIM X(6)
50 NUM=0:NP=0
60 S$="SD.DAT"
70 WW=6
80 PRINT
90 PRINT "1--display or print station data"
100 PRINT "2--set up new SD.DAT file"
110 PRINT "3--edit data"
120 PRINT "4--add station"
130 PRINT "5--delete station"
140 PRINT "6--quit"
150 PRINT "---------------------------"
160 INPUT "Choose one of the above ",CH
170 PRINT
180 ON CH GOTO 280,210,280,280,280,2960
190 GOTO 160
200 '------------------------------------------------
210 PRINT "WARNING-- CURRENT STATION DATA FILE WILL BE DESTROYED."
220 PRINT: INPUT "DO YOU WISH TO CONTINUE (Y/N)";YN$
230 PRINT
240 IF YN$="N" OR YN$="n" THEN 2960
250 OPEN "O",#2,"COPY"
260 GOTO 1980
270 ' PRINTOUT OF DATA IN FILE
280 OPEN "i",#1,S$
290 IF EOF(1) THEN 380
300 INPUT #1,N,SN$
310 INPUT #1,IUNIT,OPTN,WAT
320 FOR J=1 TO WW
330 INPUT #1,X(J)
340 NEXT J
350 IF CH=1 THEN 430
360 PRINT N,SN$
370 GOTO 290
380 CLOSE #1
385 NMAX=N
390 IF CH=1 THEN 2850
400 IF CH=3 THEN 880
410 IF CH=4 THEN 1750
420 IF CH=5 THEN 2370
430 IF N>1 THEN 450
440 INPUT "DO YOU WANT SCREEN OR PRINTER LISTING (S/P)";YN$
450 IF YN$="P" OR YN$="p" THEN 690
460 IF YN$="S" OR YN$="s" THEN 500
470 PRINT CHR$(7)
480 PRINT "WRONG ENTRY--TRY AGAIN":PRINT
490 GOTO 440
500 PRINT
510 PRINT N,SN$
520 IF IUNIT=1 THEN PRINT "UNITS----------------------------- METRIC"
530 IF IUNIT=2 THEN PRINT "UNITS----------------------------- ENGLISH"
540 IF OPTN=2 THEN PRINT "STANDARD DEV. OF WD AVAILABLE----- YES"
550 IF OPTN=1 THEN PRINT "STANDARD DEV. OF WD AVAILABLE----- NO"
560 IF OPTN=2 THEN PRINT "STANDARD DEV. AVE. TIME (min)-----";WAT
570 PRINT "LATITUDE------------------------- ";X(1)
580 PRINT "LONGITUDE------------------------ ";X(2)
590 PRINT "SURFACE ROUGHNESS (cm)----------- ";X(3)
600 IF IUNIT=1 THEN PRINT "HEIGHT OF WIND MEASUREMENT (m)--- ";X(4)
610 IF IUNIT=2 THEN PRINT "HEIGHT OF WIND MEASUREMENT (ft)-- ";X(4)
620 PRINT "TIME DIFFERENCE(Greenwich-local)- ";X(5)
630 IF IUNIT=1 THEN PRINT "STATION ELEVATION (m)------------ ";X(6)
640 IF IUNIT=2 THEN PRINT "STATION ELEVATION (ft)----------- ";X(6)
650 IF N=2 OR N=4 OR N=6 OR N=8 THEN 660 ELSE 290
660 PRINT
670 INPUT "PRESS <RETURN> TO CONTINUE",YY$
680 GOTO 290
690 LPRINT N,SN$
700 IF IUNIT=1 THEN LPRINT "UNITS------------------------------ METRIC"
710 IF IUNIT=2 THEN LPRINT "UNITS------------------------------ ENGLISH"
720 IF OPTN=2 THEN LPRINT "STANDARD DEV. OF WD AVAILABLE------- YES"
730 IF OPTN=1 THEN LPRINT "STANDARD DEV. OF WD AVAILABLE------- NO"
740 IF OPTN=2 THEN LPRINT "STANDARD DEV. AVE. TIME (min)-------";WAT
750 LPRINT "LATITUDE------------------------- ";X(1)
760 LPRINT "LONGITUDE------------------------ ";X(2)
770 LPRINT "SURFACE ROUGHNESS (cm)----------- ";X(3)
780 IF IUNIT=1 THEN LPRINT "HEIGHT OF WIND MEASUREMENT (m)--- ";X(4)
790 IF IUNIT=2 THEN LPRINT "HEIGHT OF WIND MEASUREMENT (ft)-- ";X(4)
800 LPRINT "TIME DIFFERENCE(Greenwich-local)- ";X(5)
810 IF IUNIT=1 THEN LPRINT "STATION ELEVATION (m)----------- ";X(6)
820 IF IUNIT=2 THEN LPRINT "STATION ELEVATION (ft)---------- ";X(6)
830 LPRINT
840 GOTO 290
850 CLOSE #1
860 GOTO 2850
870 '-----------------------------------------------
880 ' CHANGE DATA in existing station file
890 '-----------------------------------------------
900 PRINT
910 INPUT "ENTER STATION NO. ",NU
920 IF NU>NMAX THEN PRINT CHR$(7): GOTO 910
930 OPEN "i",#1,S$
940 OPEN "o",#2,"copy"
950 FOR I=1 TO NU
960     INPUT #1,N,SN$
970     INPUT #1,IUNIT,OPTN,WAT
972     IF N=NU THEN 1000
980     PRINT #2,N,SN$
990     PRINT #2,IUNIT,OPTN,WAT
1000  FOR J=1 TO WW
1010     INPUT #1,X(J)
1011     IF N=NU THEN 1030
1020     PRINT #2,X(J)
1030  NEXT J
1040 NEXT I
1070 PRINT N;SN$
1080 PRINT "UNITS--------------------------- ";IUNIT
1090 PRINT "SD OPTION----------------------- ";OPTN
1100 PRINT "SD AVERAGING TIME (min)--------- ";WAT
1110 PRINT "LATITUDE------------------------ ";X(1)
1120 PRINT "LONGITUDE----------------------- ";X(2)
1130 PRINT "SURFACE ROUGHNESS (cm)---------- ";X(3)
1140 IF IUNIT=1 THEN PRINT "HEIGHT OF WIND MEASUREMENT (m)-- ";X(4)
1150 IF IUNIT=2 THEN PRINT "HEIGHT OF WIND MEASUREMENT (ft)- ";X(4)
1160 PRINT "TIME DIFFERENCE ---------------- ";X(5)
1170 IF IUNIT=1 THEN PRINT "STATION ELEVATION (m)----------- ";X(6)
1180 IF IUNIT=2 THEN PRINT "STATION ELEVATION (ft)---------- ";X(6)
1220 PRINT: PRINT
1230 INPUT "IS THIS THE STATION YOU WISH TO CHANGE(Y/N)? <Y>",YY$
1240 IF YY$="Y" OR YY$="y" OR YY$="" THEN 1370
1250 IF YY$="N" OR YY$="n" THEN 1290
1260 PRINT CHR$(7)
1270 PRINT "INCORRECT ENTRY--PLEASE TRY AGAIN"
1280 GOTO 1220
1290 CLOSE
1300 PRINT
1310 INPUT "DO YOU WISH TO TRY ANOTHER NUMBER(Y/N)";NN$
1320 IF NN$="N" OR NN$="n" THEN 2930
1330 IF NN$="Y" OR NN$="y" THEN 900
1340 PRINT CHR$(7)
1350 PRINT:PRINT "INCORRECT ENTRY--PLEASE TRY AGAIN":PRINT
1360 GOTO 1300
1370 PRINT
1380 INPUT "STATION NAME (no commas)-------------- ";SN$
1385 IF SN$="" THEN 1380
1390 INPUT "1=METRIC UNITS  2=ENGLISH UNITS------- ";IUNIT
1395 IF IUNIT<1 OR IUNIT>2 THEN 1390
1400 INPUT "STANDARD DEVIATION OF WD (Y/N)-------- ";YY$
1410 IF YY$="N" OR YY$="n" THEN WAT=0: GOTO 1430
1420 INPUT "STANDARD DEVIATION AVE. TIME (min)---- ";WAT
1430 INPUT "LATITUDE (deg)(- if S of equator)----- ";X(1)
1440 INPUT "LONGITUDE (deg)(- if E of Greenwich)-- ";X(2)
1450 INPUT "SURFACE ROUGHNESS (cm)---------------- ";X(3)
1460 IF IUNIT=1 THEN INPUT "HEIGHT OF WIND MEASUREMENT (m)-------------- ";X(4)
1470 IF IUNIT=2 THEN INPUT "HEIGHT OF WIND MEASUREMENT (ft)------------- ";X(4)
1480 INPUT "TIME DIFFERENCE(Greenwich-local standard)--- ";X(5)
1490 IF IUNIT=1 THEN INPUT "STATION ELEVATION (m)----------------------- ";X(6)
1500 IF IUNIT=2 THEN INPUT "STATION ELEVATION (ft)---------------------- ";X(6)
1510 PRINT
1520 INPUT "ARE THESE DATA CORRECT(Y/N)? <Y>",YN$
1530 IF YN$="N" OR YN$="n" THEN 1370
1540 IF YN$="Y" OR YN$="y" OR YN$="" THEN 1580
1550 PRINT CHR$(7)
1560 PRINT "WRONG ENTRY-- PLEASE TRY AGAIN":PRINT
1570 GOTO 1520
1580 IF YY$="Y" OR YY$="y" THEN OPTN=2 ELSE OPTN=1
1590 PRINT #2,N,SN$
1600 PRINT #2,IUNIT,OPTN,WAT
1610 FOR I=1 TO WW
1620     PRINT #2,X(I)
1630 NEXT I
1640 IF EOF(1) THEN 2700
1650 INPUT #1,N,SN$
1660 INPUT #1,IUNIT,OPTN,WAT
1670 PRINT #2,N,SN$
1680 PRINT #2,IUNIT,OPTN,WAT
1690 FOR I=1 TO WW
1700     INPUT #1,X(I)
1710     PRINT #2,X(I)
1720 NEXT I
1730 GOTO 1640
1740 '----------------------------------------------------
1750 'ADD DATA to file
1760 '----------------------------------------------------
1770 PRINT
1780 PRINT "AFTER WHAT STATION NUMBER DO YOU WISH TO ADD NEW STATION?"
1790 INPUT "(ENTER 0 IF YOU WANT NEW STATION TO BE FIRST)";NUM
1800 OPEN "i",#1,S$
1810 OPEN "o",#2,"copy"
1820 IF NUM>0 THEN 1840
1830 GOTO 1980
1840 IF EOF(1) AND NP=1 THEN 2700
1850 IF EOF(1) THEN 1970
1860 INPUT #1,N,SN$
1870 INPUT #1,IUNIT,OPTN,WAT
1880 IF NP=0 THEN PRINT #2,N ELSE PRINT #2,N+1
1890 PRINT #2,SN$
1900 PRINT #2,IUNIT,OPTN,WAT
1910 FOR I=1 TO WW
1920     INPUT #1,X(I)
1930     PRINT #2,X(I)
1940 NEXT I
1950 IF NUM=N THEN 1980
1960 GOTO 1840
1970 NUM=N
1980 CLS
1990 N=NUM+1
2000 INPUT "STATION NAME (no commas)-------------------- ";SN$
2010 INPUT "1-METRIC UNITS  2=ENGLISH UNITS------------- ";IUNIT
2015 IF IUNIT<1 OR IUNIT>2 THEN 2010
2020 INPUT "STANDARD DEVIATION OF WD (Y/N)-------------- ";YY$
2030 IF YY$="N" OR YY$="n" THEN WAT=0: GOTO 2050
2040 INPUT "STANDARD DEVIATION AVE. TIME (min)---------- ";WAT
2050 INPUT "LATITUDE (deg)(- if S of equator)----------- ";X(1)
2060 INPUT "LONGITUDE (deg)(- if E of Greenwich)-------- ";X(2)
2070 GOSUB 2970
2080 IF WAT=0 THEN LOCATE 6,1 ELSE LOCATE 7,1
2090 INPUT "SURFACE ROUGHNESS (cm)---------------------- ";X(3)
2100 IF IUNIT=1 THEN INPUT "HEIGHT OF WIND MEASUREMENT (m)-------------- ";X(4)
2110 IF IUNIT=2 THEN INPUT "HEIGHT OF WIND MEASUREMENT (ft)------------- ";X(4)
2120 INPUT "TIME DIFFERENCE (Greenwich-local standard)-- ";X(5)
2130 IF IUNIT=1 THEN INPUT "STATION ELEVATION (m)----------------------- ";X(6)
2140 IF IUNIT=2 THEN INPUT "STATION ELEVATION (ft)---------------------- ";X(6)
2150 PRINT
2160 INPUT "ARE DATA CORRECT(Y/N)? <Y>",NY$
2170 IF NY$="Y" OR NY$="y" OR NY$="" THEN 2220
2180 IF NY$="N" OR NY$="n" THEN 1980
2190 PRINT CHR$(7)
2200 PRINT:PRINT "WRONG ENTRY--ENTER N OR Y"
2210 GOTO 2160
2220 IF YY$="Y" OR YY$="y" THEN OPTN=2 ELSE OPTN=1
2230 PRINT #2,N,SN$
2240 PRINT #2,IUNIT,OPTN,WAT
2250 FOR I=1 TO WW
2260     PRINT #2,X(I)
2270 NEXT I
2280 NP=1
2290 IF CH<>2 THEN 1840
2300 PRINT
2310 INPUT "DOES STATION DATA FILE CURRENTLY EXIST (Y/N)";YN$
2320 IF YN$="Y" OR YN$="y" THEN 2700
2330 CLOSE: GOTO 2730
2340 '---------------------------------------------
2350 'DELETE DATA  to file
2360 '---------------------------------------------
2370 PRINT
2380 INPUT "ENTER STATION NO. TO BE DELETED ",NUM
2390 OPEN "i",#1,S$
2400 OPEN "o",#2,"copy"
2410 IF EOF(1) THEN 2700
2420 INPUT #1,N,SN$
2430 INPUT #1,IUNIT,OPTN,WAT
2440 FOR I=1 TO WW
2450      INPUT #1,X(I)
2460 NEXT I
2470 IF NUM=N THEN 2550
2480 IF NP=0 THEN PRINT #2,N ELSE PRINT #2,N-1
2490 PRINT #2,SN$
2500 PRINT #2,IUNIT,OPTN,WAT
2510 FOR I=1 TO WW
2520     PRINT #2,X(I)
2530 NEXT I
2540 GOTO 2410
2550 PRINT: PRINT N;SN$
2560 INPUT "IS THIS THE STATION YOU WISH TO DELETE(Y/N)? <Y>",YY$
2570 IF YY$="Y" OR YY$="y" OR YY$="" THEN 2680
2580 IF YY$="N" OR YY$="n" THEN CLOSE: GOTO 2370
2590 PRINT CHR$(7)
2600 PRINT:PRINT "WRONG ENTRY--TRY AGAIN":PRINT
2610 GOTO 2560
2620 PRINT #2,N,SN$
2630 PRINT #2,IUNIT,OPTN,WAT
2640 FOR I=1 TO WW
2650     PRINT #2,X(I)
2660 NEXT I
2670 GOTO 2410
2680 NP=1
2690 GOTO 2410
2700 CLOSE
2710 KILL S$
2720 CLS
2730 NAME "copy" AS S$
2740 PRINT: PRINT "NEW STATION FILE CONTAINS THE FOLLOWING STATIONS:"
2750 OPEN "I",#1,S$
2760 IF EOF(1) THEN 2840
2770 INPUT #1,N,SN$
2780 INPUT #1,IUNIT,OPTN,WAT
2790 PRINT N,SN$
2800 FOR I=1 TO WW
2810     INPUT #1,X(I)
2820 NEXT I
2830 GOTO 2760
2840 CLOSE
2850 PRINT
2860 NP=0
2870 IF CH<>5 THEN 2900
2880 INPUT "DO YOU WISH TO DELETE ANOTHER STATION (Y/N) <N>";NY$
2890 IF NY$="Y" OR NY$="y" THEN 2370 ELSE 2930
2900 IF CH=4 OR CH=2 THEN 2910 ELSE 2930
2910 INPUT "DO YOU WISH TO ADD ANOTHER STATION (Y/N) <N>";NY$
2920 IF NY$="Y" OR NY$="y" THEN 1770 ELSE 2930
2930 PRINT
2940 INPUT "DO YOU WISH TO TRY A DIFFERENT OPTION (Y/N) <N>";NY$
2950 IF NY$="Y" OR NY$="y" THEN CLS: GOTO 50
2960 END
2970 'SUBROUTINE ROUGHNESS
2980 '------------------------------------------------------------------------
2990 'This subroutine displays the surface roughness for different terrain types
3000 '-------------------------------------------------------------------------
3010 LOCATE 16,1
3020 PRINT "--------------------------------------------------------------------------"
3030 PRINT "           TERRAIN DESCRIPTION                         SURFACE ROUGHNESS (cm)
3040 PRINT "SNOW, NO VEGETATION, MUD FLATS, NO OBSTACLES                      0.5"
3050 PRINT "RUNWAY, OPEN FLAT TERRAIN, GRASS, FEW ISOLATED OBSTACLES          3"
3060 PRINT "LOW CROPS, OCCASIONAL LARGE OBSTACLES                            10"
3070 PRINT "HIGH CROPS, SCATTERED OBSTACLES                                  25"
3080 PRINT "PARKLAND, BUSHES, NUMEROUS OBSTACLES                             50"
3090 PRINT "REGULAR LARGE OBSTACLE COVERAGE (SUBURB, FOREST)                100"
3100 RETURN
